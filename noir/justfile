# Kamiyo Noir Circuits

set dotenv-load

default:
    @just --list

# Install dependencies
install:
    cd lib && npm install

# Compile all circuits
compile-all:
    nargo compile --package oracle_vote
    nargo compile --package smt_exclusion
    nargo compile --package aggregate_vote
    nargo compile --package reputation_proof

# Test all circuits
test-all:
    nargo test --package oracle_vote
    nargo test --package smt_exclusion
    nargo test --package aggregate_vote
    nargo test --package reputation_proof

# Build TypeScript library
build-lib:
    cd lib && npm run build

# Build Solana program
build-program:
    cd programs/noir-verifier && cargo build-sbf

# Full build
build: compile-all build-lib

# Setup proving keys
setup circuit:
    cd circuits/{{circuit}} && sunspot setup

setup-all:
    just setup oracle-vote
    just setup smt-exclusion
    just setup aggregate-vote
    just setup reputation-proof

# Generate proofs
prove circuit:
    cd circuits/{{circuit}} && nargo execute && sunspot prove

# Verify proofs
verify circuit:
    cd circuits/{{circuit}} && sunspot verify

# Build verifier programs for deployment
build-verifiers:
    cd circuits/oracle-vote && sunspot build-verifier
    cd circuits/smt-exclusion && sunspot build-verifier
    cd circuits/aggregate-vote && sunspot build-verifier
    cd circuits/reputation-proof && sunspot build-verifier

# Deploy to devnet
deploy-devnet keypair:
    solana program deploy \
        --keypair {{keypair}} \
        --url devnet \
        programs/noir-verifier/target/deploy/noir_verifier.so

# Run example
run-example:
    cd examples && npx ts-node dispute-resolution.ts

# Clean
clean:
    rm -rf circuits/*/target
    rm -rf lib/dist
    rm -rf programs/noir-verifier/target
    rm -f circuits/*/Prover.toml

# Format
fmt:
    nargo fmt

# Check
check:
    nargo check

# CI
ci: check test-all build
